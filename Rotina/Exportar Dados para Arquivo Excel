Public Function ExportaTabelasArquivoExcel(NomeDaConsulta As Variant, CaminhoRede As String, NomeArquivo As String, NomeAbaArquivo As String, NomeArquivoBase As String)
'Antes de testar vá em Ferramentas > Referências e selecione: Microsoft Scripting Runtime e Microsoft Excel 16.0 Object Library
'Esta rotina tem como objetivo substituir dados do Access em um arquivo Excel existente, apagando os dados atuais (Sem remover os títulos) e inserindo novos dados

'Chama a sub rotina | Atualiza dados de um arquivo existente
'Call ExportaTabelasArquivoExcel(*** TABELA ***, CaminhoRede, NomeArquivo, "NOME DA ABA", "NOME DO ARQUIVO")
        

'Declara variáveis locais
Dim CaminhoLocal As String
Dim rstDados As DAO.Recordset
Dim Excel_WB As Excel.Workbook
Dim Excel_WS As Excel.Worksheet
Dim qdfConsulta As DAO.QueryDef
Dim Coluna As Integer
Dim QtdeLinhasDaTabela As Integer
Dim QtdeColunasDaTabela As Integer
Dim Excel_App As New Excel.Application



'_____________________________ ATRIBUI VALOR AS VARIÁVEIS _____________________________
Coluna = 1
CaminhoLocal = CurrentProject.Path & "\"
Set qdfConsulta = CurrentDb.QueryDefs(NomeDaConsulta) 'Atribui a variável a consulta
Set rstDados = qdfConsulta.OpenRecordset() 'Atribui a variável o resultado da consulta



'_____________________________ ROTINA PRINCIPAL _____________________________
'Exibe o arquivo
Excel_App.Visible = False

'Ativa/Desativa os alertas
Excel_App.EnableEvents = False

'Abre o arquivo para atualizar seus dados
Set Excel_WB = Excel_App.Workbooks.Open(CaminhoLocal & NomeArquivoBase)

'Atribui a aba para o arquivo focar nela
Set Excel_WS = Excel_WB.Sheets(NomeAbaArquivo)

'Foca para a aba selecionada
Excel_WS.Select

'Apaga todas as informações da aba se tem dados na célula A2
If IsEmpty(Excel_WS.Range("A2").Value) = False Then

    'Atribui a variável a qtde de linhas preenchidas na aba
    QtdeLinhasDaTabela = Excel_WS.Range("A1000000").End(xlUp).Row
    
    'Atribui a variável a qtde de colunas preenchidas na aba
    QtdeColunasDaTabela = Excel_WS.Range("A2").End(xlToRight).Column
    
    'Apaga o espaço identificado como preenchido
    Excel_WS.Range(Excel_WS.Cells(2, 1), Excel_WS.Cells(QtdeLinhasDaTabela, QtdeColunasDaTabela)).Delete

End If

'Copia os dados da consulta (ACCESS) para a aba informada (EXCEL)
Excel_WS.Range("A2").CopyFromRecordset rstDados

'Seleciona todas as células da aba ativa
Excel_WS.Cells.Select
  
'Ajusta a largura das colunas para o tamanho do conteúdo
Excel_WS.Cells.EntireColumn.AutoFit
    
'Ajusta a altura das linhas para o tamanho do conteúdo
Excel_WS.Cells.EntireRow.AutoFit

'Renomeia a aba antes de salvar como uma cópia
Excel_WS.Name = NomeAbaArquivo

'Seleciona a primeira célula da aba
Excel_WS.Range("A1").Select

'Atualiza tudo
Excel_WB.RefreshAll



'_____________________________  CRIA UMA CÓPIA NA REDE _____________________________
'Cria o link do arquivo que será anexo ao e-mail
ArquivoAnexo = CaminhoRede & Replace(NomeArquivo, "/", "")

'Cria uma cópia do arquivo na rede com o nome especificado
Excel_WB.SaveAs ArquivoAnexo, xlOpenXMLWorkbook



'_____________________________  ENCERRA O PROCESSO _____________________________
'Fecha a consulta
rstDados.Close

'Salva o arquivo
Excel_WB.Save

'Fecha o arquivo
Excel_WB.Close

'Ativa/Desativa os alertas
Excel_App.EnableEvents = True

'Exibe o arquivo
Excel_App.Visible = False



'_____________________________  ENCERRA O EXCEL _____________________________
Excel_App.Quit



'_____________________________  LIMPA AS VARIÁVEIS _____________________________
Set Excel_WB = Nothing
Set rstDados = Nothing
Set Excel_App = Nothing
Set qdfConsulta = Nothing
Set NomeDaColuna = Nothing
Set TituloDaColuna = Nothing
       
End Function
